import com.diffplug.spotless.LineEnding

plugins {
    id "java-library"
    id "maven-publish"
    id "com.diffplug.spotless" version "7.2.1"
}

group = "com.github.malczuuu"

/**
 * Conditional approach to versioning. If `-Pversion=...` is specified on the command line, it will be used as the
 * version. Otherwise, the version is derived from the git tag and build number.
 */
if (project.hasProperty("version")) {
    version = project.property("version")
} else {
    version = Versioning.getVersion(rootProject.rootDir)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven { url = "https://jitpack.io/" }
}

dependencies {
    api("org.springframework.boot:spring-boot-autoconfigure:3.5.5")
    api("org.springframework:spring-webmvc:6.2.10")
    api("org.slf4j:slf4j-api:2.0.17")
    api("com.github.malczuuu:problem4j-jackson:3.1.0")

    compileOnly("jakarta.servlet:jakarta.servlet-api:6.1.0")
    compileOnly("jakarta.validation:jakarta.validation-api:3.1.1")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor:3.5.5")

    testImplementation("jakarta.servlet:jakarta.servlet-api:6.1.0")
    testImplementation("jakarta.validation:jakarta.validation-api:3.1.1")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.13.4")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.13.4")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.13.4")

    testImplementation("org.springframework.boot:spring-boot-starter-test:3.5.5")
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

spotless {
    format "misc", {
        target "*.gradle", ".gitattributes", ".gitignore"

        trimTrailingWhitespace()
        leadingTabsToSpaces(4)
        endWithNewline()
        lineEndings = LineEnding.UNIX
    }

    format "yaml", {
        target "**/*.yml", "**/*.yaml"

        trimTrailingWhitespace()
        leadingTabsToSpaces(2)
        endWithNewline()
        lineEndings = LineEnding.UNIX
    }

    java {
        target "src/**/*.java"

        googleJavaFormat("1.28.0")
        lineEndings = LineEnding.UNIX
    }

    groovy {
        target "src/**/*.groovy"

        greclipse()
        lineEndings = LineEnding.UNIX
    }
}

tasks.register("printVersion") {
    doLast {
        println "Project version: ${Versioning.getVersion(rootProject.rootDir)}"
    }
}

tasks.named("test") {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-parameters"
}
