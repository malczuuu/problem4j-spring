plugins {
    id "java-library"
    id "maven-publish"
    id "com.diffplug.spotless" version "7.1.0"
    id "net.nemerosa.versioning" version "3.1.0"
}

group = "com.github.malczuuu"

/**
 * Conditional approach to versioning. If `-Pversion=...` is specified on the command line, it will be used as the
 * version. Otherwise, the version is derived from the git tag and build number.
 */
if (project.hasProperty("version")) {
    version = project.property("version")
} else {
    def base = versioning.info.tag ?: "${versioning.info.lastTag}-${versioning.info.build}"
    version = base + (versioning.info.dirty ? "-dirty" : "")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io/" }
}

dependencies {
    api("org.springframework.boot:spring-boot-autoconfigure:3.4.5")
    api("org.springframework:spring-webmvc:6.2.7")
    api("org.slf4j:slf4j-api:2.0.17")
    api("com.github.malczuuu:problem4j-jackson:3.1.0")

    compileOnly("jakarta.servlet:jakarta.servlet-api:6.1.0")
    compileOnly("jakarta.validation:jakarta.validation-api:3.0.2")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor:3.4.5")

    testImplementation("jakarta.servlet:jakarta.servlet-api:6.1.0")
    testImplementation("jakarta.validation:jakarta.validation-api:3.0.2")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.12.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.12.2")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.12.2")

    testImplementation("org.springframework.boot:spring-boot-starter-test:3.4.5") {
        exclude group: "junit", module: "junit"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

import com.diffplug.spotless.LineEnding

spotless {
    format "misc", {
        target "*.gradle", ".gitattributes", ".gitignore"

        trimTrailingWhitespace()
        leadingTabsToSpaces(4)
        endWithNewline()
    }

    format "yaml", {
        target "**/*.yml", "**/*.yaml"

        trimTrailingWhitespace()
        leadingTabsToSpaces(2)
        endWithNewline()
    }

    java {
        target "src/**/*.java"

        googleJavaFormat("1.28.0")
        lineEndings = LineEnding.UNIX
    }
}

tasks.named("test") {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-parameters"
}
